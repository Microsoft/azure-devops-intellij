plugins {
    id "base"
    id "de.undercouch.download"
}

def sdkArchiveLocation = "$buildDir/TFS-SDK-14.134.0.zip"
def sdkUnpackLocation = "$buildDir/sdk"
def sdkJarName = "com.microsoft.tfs.sdk-14.134.0.jar"

task downloadArchive(type: Download) {
    src "https://github.com/microsoft/team-explorer-everywhere/releases/download/14.134.0/TFS-SDK-14.134.0.zip"
    dest sdkArchiveLocation
    overwrite false
}

task verifyArchive(type: Verify, dependsOn: downloadArchive) {
    src sdkArchiveLocation
    algorithm "SHA-256"
    checksum "A7CD155475C034CB9A858B9497C473394B08D77DE90AB0A0325CF97F9ABA2055"
}

task unpack(type: Copy, dependsOn: [downloadArchive, verifyArchive]) {
    from(zipTree(sdkArchiveLocation)) {
        include "TFS-SDK-14.134.0/redist/lib/*.jar"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(3))
        }
        includeEmptyDirs = false
    }
    into sdkUnpackLocation
}

artifacts.add("default", file("$sdkUnpackLocation/$sdkJarName")) {
    builtBy unpack
}