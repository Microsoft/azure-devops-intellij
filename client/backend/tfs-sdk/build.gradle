// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See License.txt in the project root.

plugins {
    id "base"
    id "de.undercouch.download"
}

ext {
    sdkVersion = "14.134.0"
    sdkArchiveLocation = file("$buildDir/TFS-SDK-${sdkVersion}.zip")
    sdkUnpackLocation = file("$buildDir/sdk")
    sdkJarName = "com.microsoft.tfs.sdk-${sdkVersion}.jar"
}

task downloadArchive(type: Download) {
    src "https://ci.appveyor.com/api/buildjobs/n47diettm0kvax1g/artifacts/build%2Foutput%2Fbin%2Fsdk%2FTFS-SDK-14.134.0.zip"
    dest sdkArchiveLocation
    overwrite false
}

task verifyArchive(type: Verify, dependsOn: downloadArchive) {
    src sdkArchiveLocation
    algorithm "SHA-256"
    checksum "24411accb563560d31b14139676dba2ba696a78e8e9396838169a256c25db077"
}

task unpack(type: Copy, dependsOn: [downloadArchive, verifyArchive]) {
    from(zipTree(sdkArchiveLocation)) {
        include "TFS-SDK-$sdkVersion/redist/lib/*.jar"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(3))
        }
        includeEmptyDirs = false
    }
    into sdkUnpackLocation
}

artifacts.add("default", file("$sdkUnpackLocation/$sdkJarName")) {
    builtBy unpack
}