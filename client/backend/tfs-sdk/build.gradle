// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See License.txt in the project root.

plugins {
    id "base"
    id "de.undercouch.download"
}

ext {
    sdkVersion = "14.135.0"
    sdkArchiveLocation = file("$buildDir/TFS-SDK-${sdkVersion}.zip")
    sdkUnpackLocation = file("$buildDir/sdk")
    sdkJarName = "com.microsoft.tfs.sdk-${sdkVersion}.jar"
}

task downloadArchive(type: Download) {
    src "https://github.com/JetBrains/team-explorer-everywhere/releases/download/$sdkVersion/TFS-SDK-${sdkVersion}.zip"
    dest sdkArchiveLocation
    overwrite false
}

task verifyArchive(type: Verify, dependsOn: downloadArchive) {
    src sdkArchiveLocation
    algorithm "SHA-256"
    checksum "6C7600C40248F986F873829B88429FCACAFFD0CCB70DD7D9F11CB7D4BAA2103D"
}

task unpack(type: Copy, dependsOn: [downloadArchive, verifyArchive]) {
    from(zipTree(sdkArchiveLocation)) {
        include "TFS-SDK-$sdkVersion/redist/lib/*.jar"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(3))
        }
        includeEmptyDirs = false
    }
    into sdkUnpackLocation
}

artifacts.add("default", file("$sdkUnpackLocation/$sdkJarName")) {
    builtBy unpack
}